{
  config,
  lib,
  dream2nix,
  ...
}: let
  rev = "29e62db";
  src = config.deps.fetchFromGitHub {
    owner = "PokeAPI";
    repo = "pokeapi";
    inherit rev;
    sha256 = "sha256-VFYj98KQKU4tpNuqedVzf8sAomV0BkIOamHuvaLsPeU=";
    fetchSubmodules = true;
  };
in {
  imports = [
    dream2nix.modules.dream2nix.pip
  ];

  deps = {nixpkgs, ...}: {
    inherit (nixpkgs) fetchFromGitHub python313Packages;
  };

  name = "pokeapi";
  version = rev;

  mkDerivation = {
    inherit src;
    installPhase = ''
      mkdir -p $out
    '';
    buildPhase = ''
      make setup
      make build-db
      mkdir -p $out
      cp db.sqlite3 $out/
      cp -r data/v2/sprites/sprites $out/
    '';
  };

  buildPythonPackage = {
    pyproject = false;
  };

  # Usually this lock file generated by dream2nix needs to specify the system,
  # but all the dependencies should be system-independent.
  paths.lockFile = "pokeapi.lock.json";
  pip = {
    requirementsFiles = ["${src}/requirements.txt"];
    flattenDependencies = true;
    overrides =
      builtins.foldl' (acc: package:
        acc
        // {
          ${package}.buildPythonPackage = {
            pyproject = true;
            build-system = [config.deps.python313Packages.setuptools];
          };
        }) {
      } [
        "coverage"
        "mimeparse"
        "unipath"
      ];
  };
}
